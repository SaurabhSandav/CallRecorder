// Top-level build file where you can add configuration options common to all sub-projects/modules.

plugins {
    id 'dependencies'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'com.android.application' version '4.2.0-alpha11' apply false
    id 'org.jetbrains.kotlin.android' version '1.4.10' apply false
    id 'com.squareup.sqldelight' version '1.4.3' apply false
    id 'dagger.hilt.android.plugin' version '2.29.1-alpha' apply false
}

allprojects {

    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url "https://jitpack.io" } // For compose-navigation
        maven { url "https://kotlin.bintray.com/kotlinx/" } // For kotlinx-datetime

        if (GoogleMaven.SUPPORT_REPO_ENABLED) {
            maven { url GoogleMaven.SUPPORT_REPO_URL }
        }

        if (GoogleMaven.UI_REPO_ENABLED) {
            maven { url GoogleMaven.UI_REPO_URL }
        }

        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    '-progressive',
                    '-Xinline-classes',
                    '-Xskip-prerelease-check',
                    '-Xallow-jvm-ir-dependencies',
                    '-Xopt-in=kotlin.ExperimentalStdlibApi',
                    "-Xopt-in=kotlin.time.ExperimentalTime",
                    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi',
                    '-Xopt-in=kotlinx.coroutines.FlowPreview',
                    '-Xopt-in=androidx.compose.runtime.ExperimentalComposeApi',
                    '-Xopt-in=androidx.compose.foundation.layout.ExperimentalLayout',
                    '-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi',
                    '-Xopt-in=androidx.compose.material.ExperimentalMaterialApi',
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
